group 'com.example.dengage_flutter'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.9.22'
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 34
    namespace 'com.example.dengage_flutter'
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
}

def dengageVersion = rootProject.hasProperty('dengageSdkVersion') ? rootProject.getProperty('dengageSdkVersion') : '6.0.81'
def useHms = rootProject.hasProperty('useHmsSdk') && rootProject.getProperty('useHmsSdk') == 'true'
def useGeofence = rootProject.hasProperty('useGeofenceSdk') && rootProject.getProperty('useGeofenceSdk') == 'true'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation "com.github.dengage-tech.dengage-android-sdk:sdk:$dengageVersion"
    if (useGeofence) {
        implementation "com.github.dengage-tech.dengage-android-sdk:sdk-geofence:$dengageVersion"
    }
    if (useHms) {
        implementation "com.github.dengage-tech.dengage-android-sdk:sdk-hms:$dengageVersion"
    }
    implementation 'com.google.errorprone:error_prone_annotations:2.23.0'
}
